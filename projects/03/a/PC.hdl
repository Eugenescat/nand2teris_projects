// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl
/**
 * A 16-bit counter with reset, load, and inc control bits.
 * if      (reset(t) == 1) out(t+1) = 0
 * else if (load(t) == 1)  out(t+1) = in(t)
 * else if (inc(t) == 1)   out(t+1) = out(t) + 1
 * else                    out(t+1) = out(t)
 */

CHIP PC {
    IN in[16], load, inc, reset;
    OUT out[16];

    PARTS:
    Inc16(in=out0 ,out=outPlus);  
    Mux16(a=out0, b=outPlus, sel=inc, out=outInc);

    Mux16(a=outInc, b=in, sel=load, out=outLoad);

    Mux16(a=outLoad, b[0..15]=false, sel=reset, out=out1);

   /** 
    * 单纯就是模拟了一个clock，真正的register的循环作用实际上是通过out0实现的
    * 想要真正使用register的循环是不可能的，因为这会导致out0只能被load操作，无法加入其他reset和Inc的操作
    */
    Register(in=out1, load=true, out=out, out=out0);
}